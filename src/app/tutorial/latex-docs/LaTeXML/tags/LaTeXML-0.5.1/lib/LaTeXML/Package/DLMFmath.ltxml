# -*- CPERL -*-
# /=====================================================================\ #
# |  DLMFmath                                                           | #
# | Implementation for LaTeXML                                          | #
# |=====================================================================| #
# | Part of LaTeXML:                                                    | #
# |  Public domain software, produced as part of work done by the       | #
# |  United States Government & not subject to copyright in the US.     | #
# |---------------------------------------------------------------------| #
# | Bruce Miller <bruce.miller@nist.gov>                        #_#     | #
# | http://dlmf.nist.gov/LaTeXML/                              (o o)    | #
# \=========================================================ooo==U==ooo=/ #

# Currently only a random collection of things I need for DLMF chapters.
# Eventually, go through the doc and implement it all.
#**********************************************************************
package LaTeXML::Package::Pool;
use strict;
use LaTeXML::Package;

#**********************************************************************
# Load special functions & other required packages.
RequirePackage('amssymb');
RequirePackage('keyval');

UnshiftValue(SEARCHPATHS=>"$ENV{HOME}/dlmf/styles/DLMFtex/");
RequirePackage('DLMFfcns');

#**********************************************************************
# General Math Constructs

DefMath('\Real',      "\x{211D}", role=>'ID'); # DOUBLE-STRUCK CAPITAL R
DefMath('\Complex',   "\x{2102}", role=>'ID'); # DOUBLE-STRUCK CAPITAL C
DefMath('\NatNumber', "\x{2115}", role=>'ID'); # DOUBLE-STRUCK CAPITAL N
DefMath('\Integer',   "\x{2124}", role=>'ID'); # DOUBLE-STRUCK CAPITAL Z
DefMath('\Rational',  "\x{211A}", role=>'ID'); # DOUBLE-STRUCK CAPITAL Q
DefMath('\Polynomial',"\x{2119}", role=>'ID'); # DOUBLE-STRUCK CAPITAL P

# Redefined here, since Frank doesn't like the Fraktur.
DefMath('\Re',       "Re", role=>'OPFUNCTION');
DefMath('\Im',       "Im", role=>'OPFUNCTION');

DefMath('\realpart{}', "\x{211C}");	# BLACK-LETTER CAPITAL R
DefMath('\imagpart{}', "\x{2111}");	# BLACK-LETTER CAPITAL I

DefMath('\iunit',"\x{2148}", role=>'ID', name=>'iunit'); # DOUBLE-STRUCK ITALIC SMALL I
DefMath('\expe', "\x{2147}", role=>'ID', name=>'exponentiale'); # DOUBLE-STRUCK ITALIC SMALL E
DefMath('\cpi',  "\x{03C0}", role=>'ID', name=>'circularpi'); # pi (is there a special one?)
DefMacro('\opminus',   "(-1)");

# \cfrac covered in amsmath.
# \midvert, \midVert ??

DefMath('\floor{}','\left\lfloor#1\right\rfloor');
DefMath('\ceiling{}','\left\lceil#1\right\rceil');

DefMath('\divides','|', role=>'MULOP');
DefMath('\abs{}', '\left|#1\right|');
DefMath('\pgcd{}', '\left(#1\right)');

#**********************************************************************
# Numbers
#   \Sci{mantisa}{exponent}
my %SUPDIGITS = (0=>"\x{2070}",1=>"\x{00b9}",2=>"\x{00b2}",3=>"\x{00b3}",4=>"\x{2074}",
		 5=>"\x{2075}",6=>"\x{2076}",7=>"\x{2077}",8=>"\x{2078}",9=>"\x{2079}",
		'+'=>"\x{207a}", '-'=>"\x{207b}");
sub convert_sci {
  my($mantissa,$exponent)=@_;
  $mantissa = $mantissa->toString;
  $mantissa =~ s/\\,/\x{2009}/g;
  $mantissa =~ s/\\:/\x{2005}/g;
  $mantissa =~ s/\\;/\x{2004}/g;
  $mantissa =~ s/\\ /\x{2002}/g;
  $mantissa =~ s/\\!//g;
  $exponent = $exponent->toString;
  ($exponent
   ? join('',$mantissa,"\x{00D7}10",map($SUPDIGITS{$_}||'?',split('',$exponent))) 
   : $mantissa . "\x{00A0}\x{00A0}\x{00A0}"); } # for alignment spacing???

sub convert_sci_mantissa {
  my($mantissa)=@_;
  $mantissa = $mantissa->toString;
  $mantissa =~ s/\\,/\x{2009}/g;
  $mantissa =~ s/\\:/\x{2005}/g;
  $mantissa =~ s/\\;/\x{2004}/g;
  $mantissa =~ s/\\ /\x{2002}/g;
  $mantissa =~ s/\\!//g;
  $mantissa; }
sub convert_sci_exponent {
  my($exponent)=@_;
  $exponent = $exponent->toString;
  ($exponent
   ? join('',"\x{00D7}10",map($SUPDIGITS{$_}||'?',split('',$exponent))) 
   : ""); }

DefConstructor('\Sci{}{}',sub {
  my($document,$mantissa,$exponent,%props)=@_;
  my $string = convert_sci($mantissa,$exponent);
  ($props{isMath} 
   ? $document->insertMathToken($string,font=>$props{font},role=>'NUMBER')
   : $document->openText($string,$props{font})); });

# Worry about alignment later...
#DefMacro('\TSci{}{}','\multicolumn{2}{r}{\Sci{#1}{#2}}');

DefConstructor('\Sci@Exp{}',
	       sub { my($document,$exp,%props)=@_;
		     $document->insertMathToken(convert_sci_exponent($exp),
						font=>$props{font}, role=>'NUMBER'); },
	      reversion=>sub { my $exp = $_[0]->getArg(1);
			      ($exp->unlist ? (T_CS('\times'),T_OTHER('1'),T_OTHER('0'),
					       T_SUPER,T_BEGIN,$exp->revert,T_END)
			       :()); });
DefMacro('\TSci{}{}','\ensuremath{#1} & \ensuremath{\Sci@Exp{#2}}');

#**********************************************************************
# Calculus
# Handled as general constructors since empty numerator is effectively a differential
# operator, instead of a derivative.
DefConstructor('\deriv[]{}{}',  
	       "<ltx:XMApp ?#2()(role='DIFFOP')>"
	       . "<ltx:XMTok name='deriv' style='#style'/>"
	       . "?#2(<ltx:XMArg>#2</ltx:XMArg>)(<ltx:XMTok name='Empty'/>)"
	       . "<ltx:XMArg>#3</ltx:XMArg>"
	       . "?#1(<ltx:XMArg>#1</ltx:XMArg>)"
	       ."</ltx:XMApp>",
	       bounded=>1,beforeDigest=>\&beforeFrac, afterDigest =>\&afterFrac);
DefConstructor('\pderiv[]{}{}',
	       "<ltx:XMApp ?#2()(role='DIFFOP')>"
	       . "<ltx:XMTok name='pderiv' style='#style'/>"
	       . "?#2(<ltx:XMArg>#2</ltx:XMArg>)(<ltx:XMTok name='Empty'/>)"
	       . "<ltx:XMArg>#3</ltx:XMArg>"
	       . "?#1(<ltx:XMArg>#1</ltx:XMArg>)"
	       ."</ltx:XMApp>",
	       bounded=>1,beforeDigest=>\&beforeFrac, afterDigest =>\&afterFrac);
DefConstructor('\ideriv[]{}{}',
	       "<ltx:XMApp ?#2()(role='DIFFOP')>"
	       . "<ltx:XMTok name='deriv' style='inline'/>"
	       . "?#2(<ltx:XMArg>#2</ltx:XMArg>)(<ltx:XMTok name='Empty'/>)"
	       . "<ltx:XMArg>#3</ltx:XMArg>"
	       . "?#1(<ltx:XMArg>#1</ltx:XMArg>)"
	       ."</ltx:XMApp>");
DefConstructor('\ipderiv[]{}{}',
	       "<ltx:XMApp ?#2()(role='DIFFOP')>"
	       . "<ltx:XMTok name='pderiv' style='inline'/>"
	       . "?#2(<ltx:XMArg>#2</ltx:XMArg>)(<ltx:XMTok name='Empty'/>)"
	       . "<ltx:XMArg>#3</ltx:XMArg>"
	       . "?#1(<ltx:XMArg>#1</ltx:XMArg>)"
	       ."</ltx:XMApp>");
DefConstructor('\diff[]{}',
	       "<ltx:XMApp>"
	       . "<ltx:XMTok name='diff'/>"
	       . "<ltx:XMArg>#2</ltx:XMArg>"
	       . "?#1(<ltx:XMArg>#1</ltx:XMArg>)"
	       ."</ltx:XMApp>");
DefConstructor('\pdiff[]{}',
	       "<ltx:XMApp>"
	       . "<ltx:XMTok name='pdiff'/>"
	       . "<ltx:XMArg>#2</ltx:XMArg>"
	       . "?#1(<ltx:XMArg>#1</ltx:XMArg>)"
	       ."</ltx:XMApp>");
DefConstructor('\qdiff{}{}',
	       "<ltx:XMApp>"
	       . "<ltx:XMTok name='qdiff'/>"
	       . "<ltx:XMArg>#1</ltx:XMArg>"
	       . "<ltx:XMArg>#2</ltx:XMArg>"
	       ."</ltx:XMApp>");


# Forward, Backward and Central differences operator
DefMath('\fDiff []','\Delta_{#1}',role=>'DIFFOP');
DefMath('\bDiff []','\nabla_{#1}',role=>'DIFFOP');
DefMath('\cDiff []','\delta_{#1}',role=>'DIFFOP');
#**********************************************************************
DefMath('\pvint',  "\x{2A0D}", name=>'PrincipalValueIntegral', role=>'INTOP'); # FINITE PART INTEGRAL
DefMath('\Residue', "res",     name=>"Residue", role=>'LIMITOP');

#**********************************************************************
# Types

DefMath('\Vector{}','\boldsymbol{#1}', role=>'ID');
DefMath('\Matrix{}','\boldsymbol{#1}', role=>'ID');
DefMacro('\Vector{}','\ROLE{ID}{\boldsymbol{#1}}');
DefMacro('\Matrix{}','\ROLE{ID}{\boldsymbol{#1}}');

DefMath('\divergence', "div",  role=>'DIFFOP');
DefMath('\curl',       "curl", role=>'DIFFOP');
DefMath('\gradient',   "grad", role=>'DIFFOP');
DefMath('\transpose{}','#1^{\mathrm{T}}');
DefMath('\trace',      "tr",   role=>'LIMITOP');
DefMath('\diag',       "diag", role=>'LIMITOP');

#**********************************************************************
# Combinatorics
DefMath('\binomial{}{}',    '', name=>'binomial');
DefMath('\multinomial{}{}', '\left(#1;#2\right)');
DefMath('\pochhammer{}{}',  '\left(#1\right)_{#2}');
DefMath('\psfactorial{}{}',  '\left[#1\right]_{#2}');
DefMath('\wigner{}{}{}{}{}{}', '\left(#1 #2 #3 #4 | #1 #2 #5 #6\right)');

DefMath('\qFactorial{}{}{}','\left(#1;#2\right)_{#3}');
DefMath('\qBinomial{}{}{}', '\genfrac[]\z@{}{#1}{#2}_{#3}');

DefExpandable('\threej OptionalKeyword:@',
	      sub { my($gullet,$at)=@_;
		    ($at ? (T_CS('\threej@args')):(T_CS('\threej@text'))); });
DefExpandable('\sixj OptionalKeyword:@',
	      sub { my($gullet,$at)=@_;
		    ($at ? (T_CS('\sixj@args')):(T_CS('\sixj@text'))); });
DefExpandable('\ninej OptionalKeyword:@',
	      sub { my($gullet,$at)=@_;
		    ($at ? (T_CS('\ninej@args')):(T_CS('\ninej@text'))); });
DefMath('\threej@text','3j', role=>'ID', reversion=>'3j',name=>'threej');
DefMath('\sixj@text',  '6j', role=>'ID', reversion=>'6j',name=>'sixj');
DefMath('\ninej@text', '9j', role=>'ID', reversion=>'9j',name=>'ninej');
DefMath('\threej@args {}{}{}{}{}{}',
	'\begin{pmatrix}#1&#2&#3\\\\#4&#5&#6\end{pmatrix}',
	name=>'threej');
DefMath('\sixj@args {}{}{}{}{}{}',
	'\begin{Bmatrix}#1&#2&#3\\\\#4&#5&#6\end{Bmatrix}',
	name=>'sixj');
DefMath('\ninej@args {}{}{}{}{}{}{}{}{}',
	'\begin{Bmatrix}#1&#2&#3\\\\#4&#5&#6\\\\#7&#8&#9\end{Bmatrix}',
	name=>'ninej');

#**********************************************************************
# Constants
DefMath('\EulerConstant',"\x{03B3}", role=>'ID'); # GREEK SMALL LETTER GAMMA
DefMath('\BoltzmannConstant',"k", role=>'ID');
#**********************************************************************
# Special Functions
DefPrimitive('\specFunObsolete{}{}',sub {
  my($defn,$old,$new)=@_;
  DefMacro("\\".$old->toString,"\\".$new->toString); });

sub count_tokens {
  my($gullet,$token)=@_;
  my $n=0;
  while($gullet->readMatch($token)){ $n++; }
  $n; }

#**********************************************************************

DefPrimitive('\defSpecFun{}[][]{} OptionalKeyVals:DefSpecFun {}', sub {
  my($stomach,$name,$nparam,$optparam,$formatter,$keys,$nargs)=@_;
  my $gullet = $stomach->getGullet;
  $name=$name->toString;
  my $cs = '\\'.$name;
  $nargs  = $nargs->toString;
  my $role = ($keys && ToString($keys->getValue('role'))) || 'FUNCTION';
  DefExpandable($cs.'@role',Tokens(T_OTHER($role)));
  # Define with DefMath to create presentation & content cs's
  DefMath($cs . convertLaTeXArgs($nparam,$optparam), $formatter, 
	  role=>$role, operator_role=>$role, omcd=>'dlmf');
  # Then, redefine top-level macro to check for powers, args, etc.
  DefExpandable($cs.convertLaTeXArgs($nparam,$optparam),
		sub { expand_specfun($cs,$nargs,@_); });

  # Read any argument formats & generate args macros
  my @argfmts=();
  while(my $af = $gullet->readOptional){
    push(@argfmts,$af); }
  push(@argfmts, Tokenize("\\!\\left(".join(',',map("#$_",1..$nargs))."\\right)"))
    unless @argfmts;
  for(my $i=0; $i <= $#argfmts; $i++){
    DefMacro(join('',$cs,'@args',('@' x ($i+1)),('{}' x $nargs)),$argfmts[$i]->toString); }

  $gullet->skipSpaces;
  return; });

DefConstructor('\sf@apply{}{}', "<ltx:XMApp>#1#2</ltx:XMApp>",
  reversion => sub {
    my($fcn,$args) = $_[0]->getArgs;
    ($fcn->revert,T_OTHER("@"),map( (T_BEGIN,$_->revert,T_END), $args->unlist)); });

DefExpandable('\@PRIMES{}',sub { ((T_CS('\prime')) x $_[1]->toString); });
DefMath('\diffop{}{}','{{#1}^{\@PRIMES{#2}}}');
DefMath('\power{}{}',"{#1^{#2}}");

sub expand_specfun {
  my($cs,$nargs,$gullet,@params)=@_;
  my($cparams,$pparams)=dualize_arglist(@params);
  my $ccall = Invocation(T_CS($cs.'@content'),@$cparams);
  my $pcall = Invocation(T_CS($cs.'@presentation'),@$pparams);
  $gullet->skipSpaces;
  # Check for following primes and superscripts, and wrap the function appropriately.
  while(my $op = $gullet->readMatch(T_OTHER("'"),T_SUPER)){ # Hmm, ' is active only in math?
    if($op->equals(T_SUPER)){
      # NOTE: Before I go assuming that ^{x} is a power, consider analyzing for things like ^{(n)} !!!
      my($cpower,$ppower) = dualize_arglist($gullet->readArg); # Should do xmref here too!!!
      $ccall = Invocation(T_CS('\power@content'),$ccall,@$cpower);
      $pcall = Invocation(T_CS('\power@presentation'),$pcall,@$ppower); }
    else{
      my $n = 1+count_tokens($gullet,$op);
      $ccall = Invocation(T_CS('\diffop@content'),$ccall,T_OTHER($n));
      $pcall = Invocation(T_CS('\diffop@presentation'),$pcall,T_OTHER($n));}}
  $gullet->skipSpaces;
  # Check for function arguments
  if(my $variant = count_tokens($gullet,T_OTHER('@'))){
    my $argsmacro = LookupDefinition(T_CS($cs.'@args'.('@' x $variant)));
    my($cargs,$pargs)=dualize_arglist($argsmacro->readArguments($gullet));
    Invocation(T_CS('\DUAL'),
	       T_OTHER('ATOM'),
	       Invocation(T_CS('\sf@apply'),$ccall,Tokens(map((@$_),@$cargs))),
	       Tokens($pcall->unlist,T_CS('\@APPLYFUNCTION'),
		      $argsmacro->invocation(@$pargs))
		  )->unlist; }
  else {
##    Invocation(T_CS('\DUAL'),
##	       T_OTHER(($nargs ? 'FUNCTION' : 'ATOM')), $ccall, $pcall)->unlist}; }
    Invocation(T_CS('\DUAL'),T_CS($cs.'@role'), $ccall, $pcall)->unlist}; }

#**********************************************************************
1;

